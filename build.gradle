import org.jetbrains.intellij.platform.gradle.TestFrameworkType

plugins {
    id 'java'
    id("org.jetbrains.intellij.platform") version "2.10.5"
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'org.jetbrains.intellij.platform'

group = 'cn.sticki'
version = '1.0.0'

repositories {
    mavenCentral()
    intellijPlatform {
        defaultRepositories()
    }
}

// Simple function to load change-notes.html and description.html into valid text for plugin.xml
def htmlFixer = { f -> file(f).text.replace('<html>', '').replace('</html>', '') }

patchPluginXml {
    changeNotes = htmlFixer('change-notes.html')
}

// Configure IntelliJ Platform Gradle Plugin
// Read more: https://plugins.jetbrains.com/docs/intellij/tools-intellij-platform-gradle-plugin.html
dependencies {
    intellijPlatform {
        create('IU', '2023.2')

        bundledPlugin('com.intellij.java')
        bundledPlugin('org.intellij.intelliLang')
        bundledPlugin('com.intellij.spring')
        bundledPlugin('com.intellij.javaee.el')

        // Spring 插件可能依赖的插件
        testBundledPlugin('com.intellij.java-i18n')
        testBundledPlugin('com.intellij.properties')

        // 这两个组合会自动提供 IDEA 测试基类所需的全部依赖（含 JUnit 4）
        testFramework(TestFrameworkType.Platform.INSTANCE)
        testFramework(TestFrameworkType.Bundled.INSTANCE)
    }

    // 现代 JUnit 5 栈
    testImplementation platform('org.junit:junit-bom:5.11.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // 兼容旧版 JUnit 3/4 测试运行
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine'
    testImplementation 'junit:junit:4.13.2'
}

intellijPlatform {
    pluginConfiguration {
        name = 'SpEL Validator Support'
        version = project.version

        ideaVersion {
            sinceBuild = '232'  // 2023.2
        }
    }
}

tasks.withType(JavaCompile) {
    sourceCompatibility = '17'
    targetCompatibility = '17'
    options.encoding = 'UTF-8'
}

tasks.named('test') {
    useJUnitPlatform()
}
